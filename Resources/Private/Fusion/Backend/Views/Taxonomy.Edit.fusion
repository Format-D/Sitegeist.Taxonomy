prototype(Sitegeist.Taxonomy:Views.Module.Taxonomy.Edit) < prototype(Neos.Fusion:Component) {

    i18nMain = ${Translation.value('').package("Sitegeist.Taxonomy").source('Main')}
    i18nVocabulary = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Vocabulary')}
    i18nTaxonomy = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Taxonomy')}

    renderer = afx`
        <legend>{props.i18nMain.id('taxon')}: {taxonomy.properties.title}</legend>
        <legend @if={defaultTaxonomy}>{props.i18nMain.id('generic.default')}: {defaultTaxonomy.properties.title}</legend>

        <Neos.Fusion.Form:Form form.target.action="updateTaxonomy" form.data.properties={taxonomy.properties}>
            <Neos.Fusion.Form:Hidden field.name="taxonomy" field.value={taxonomy.contextPath} />

            <fieldset>
                <div class="neos-control-group">
                    <label class="neos-control-label" for="title">
                        {props.i18nTaxonomy.id('properties.title')}
                        <span @if={defaultTaxonomy}>: {defaultTaxonomy.properties.title}</span>
                    </label>
                    <Neos.Fusion.Form:Textarea attributes.class="neos-span6" field.name="properties[title]" />
                </div>

                <div class="neos-control-group">
                    <label class="neos-control-label" for="description">
                        {props.i18nTaxonomy.id('properties.description')}
                        <span @if={defaultTaxonomy}>: {defaultTaxonomy.properties.description}</span>
                    </label>
                    <Neos.Fusion.Form:Textarea attributes.class="neos-span6 form-inline" field.name="properties[description]"/>
                </div>

                <div class="neos-control-group">
                    <Neos.Fusion:Link.Action
                        class="neos-button"
                        href.action="vocabulary"
                        href.arguments.vocabulary={vocabulary}
                    >
                        {props.i18nMain.id('generic.cancel')}
                    </Neos.Fusion:Link.Action>
                    &nbsp;
                    <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">{props.i18nMain.id('generic.save') + ''}</Neos.Fusion.Form:Button>
                </div>
            </fieldset>
        </Neos.Fusion.Form:Form>
    `

}
