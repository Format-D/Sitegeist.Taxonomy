prototype(Sitegeist.Taxonomy:Views.Module.Vocabulary.Edit) < prototype(Neos.Fusion:Component) {

    i18nMain = ${Translation.value('').package("Sitegeist.Taxonomy").source('Main')}
    i18nVocabulary = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Vocabulary')}

    renderer = afx`
        <div class="neos-row-fluid">
            <legend>{props.i18nMain.id('vocabulary')}: {vocabulary.properties.title}</legend>
            <legend @if={defaultVocabulary}>{props.i18nMain.id('generic.default')}: {defaultVocabulary.properties.title}</legend>
        </div>

        <Neos.Fusion.Form:Form form.target.action="updateVocabulary" form.data.properties={vocabulary.properties}>
            <Neos.Fusion.Form:Hidden field.name="vocabulary" field.value={vocabulary.contextPath} />

            <fieldset>
                <div class="neos-control-group">
                    <label class="neos-control-label" for="title">
                        {props.i18nVocabulary.id('properties.title')}
                        <span @if={defaultVocabulary}>: {defaultVocabulary.properties.title}</span>
                    </label>
                    <Neos.Fusion.Form:Textarea attributes.class="neos-span6" field.name="properties[title]" />
                </div>

                <div class="neos-control-group">
                    <label class="neos-control-label" for="description">
                        {props.i18nVocabulary.id('properties.description')}
                        <span @if={defaultVocabulary}>: {defaultVocabulary.properties.description}</span>
                    </label>
                    <Neos.Fusion.Form:Textarea attributes.class="neos-span6 form-inline" field.name="properties[description]"/>
                </div>

                <div class="neos-control-group">
                    <Neos.Fusion:Link.Action
                        class="neos-button"
                        href.action="index"
                        href.arguments.root={taxonomyRoot}
                    >
                        {props.i18nMain.id('generic.cancel')}
                    </Neos.Fusion:Link.Action>
                    &nbsp;
                    <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">{props.i18nMain.id('generic.save') + ''}</Neos.Fusion.Form:Button>
                </div>
            </fieldset>
        </Neos.Fusion.Form:Form>
    `

}
