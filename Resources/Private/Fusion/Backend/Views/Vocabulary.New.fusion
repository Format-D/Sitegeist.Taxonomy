prototype(Sitegeist.Taxonomy:Views.Module.Vocabulary.New) < prototype(Neos.Fusion:Component) {

    i18nMain = ${Translation.value('').package("Sitegeist.Taxonomy").source('Main')}
    i18nVocabulary = ${Translation.value('').package("Sitegeist.Taxonomy").source('NodeTypes/Vocabulary')}

    renderer = afx`
        <legend>{props.i18nMain.id('vocabulary.create')}</legend>

        <Neos.Fusion.Form:Form form.target.action="createVocabulary">
            <Neos.Fusion.Form:Hidden field.name="taxonomyRoot" field.value={taxonomyRoot.contextPath} />
            <fieldset>
                <div class="neos-control-group">
                    <label class="neos-control-label" for="title">{props.i18nVocabulary.id('properties.title')}</label>
                    <Neos.Fusion.Form:Textarea attributes.class="neos-span6" field.name="properties[title]" />
                </div>

                <div class="neos-control-group">
                    <label class="neos-control-label" for="description">{props.i18nVocabulary.id('properties.description')}</label>
                    <Neos.Fusion.Form:Textarea attributes.class="neos-span6" field.name="properties[description]"/>
                </div>

                <div class="neos-control-group">
                    <Neos.Fusion:Link.Action
                        class="neos-button"
                        href.action="index"
                        href.arguments.root={taxonomyRoot}
                    >
                        {props.i18nMain.id('generic.cancel')}
                    </Neos.Fusion:Link.Action>
                    &nbsp;
                    <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary">{props.i18nMain.id('vocabulary.create') + ''}</Neos.Fusion.Form:Button>
                </div>
            </fieldset>
        </Neos.Fusion.Form:Form>
    `
}
